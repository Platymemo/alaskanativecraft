plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "1.2.1"
    id 'maven-publish'
    alias libs.plugins.quilt.loom
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        url = "https://maven.fabricmc.net"
    }
    maven {
        url = "https://maven.terraformersmc.com/releases"
    }
    maven {
        url = "https://maven.shedaniel.me"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft libs.minecraft
    mappings variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }

    modImplementation libs.quilt.loader
    modImplementation libs.quilted.fabric.api

    // Mod Menu integration
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    // REI integration
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    // We have to manually override the version of Architectury used by REI to run on Quilt
    // modLocalRuntime "dev.architectury:architectury-fabric:${project.architectury_override_version}"

    // ClothConfig
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
 
    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

loom {
    accessWidenerPath = file("src/main/resources/alaskanativecraft.accesswidener")
}

processResources {
    inputs.property "version", project.mod_version

    filesMatching('quilt.mod.json') {
        expand "version": project.mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // Select the repositories you want to publish to
    // To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
    repositories {
        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    }
}

import com.modrinth.minotaur.TaskModrinthUpload
import com.modrinth.minotaur.request.Dependency

task publishModrinth (type: TaskModrinthUpload) {
    onlyIf {
        System.getenv("MODRINTH")
    }

    token = System.getenv("MODRINTH")
    projectId = 'iFnEtHsI'
    versionName = "Alaska Native Craft v${project.mod_version}"
    versionNumber = "${project.mod_version}"
    uploadFile = remapJar
    changelog = new File("markdown/versions/${project.mod_version}.md").getText('UTF-8')
    addGameVersion("${libs.versions.minecraft}")
    addDependency('qsl', Dependency.DependencyType.REQUIRED)
    addDependency('cloth-config', Dependency.DependencyType.REQUIRED)
    addLoader('quilt')
}

curseforge {
    if (project.hasProperty("curse_api_key") || System.getenv("curse_api_key") != null) {
        apiKey = project.hasProperty("curse_api_key") ? project.property("curse_api_key") : System.getenv("curse_api_key")

        project {
            id = '435197'
            changelogType = 'markdown'
            changelog = file("markdown/versions/${project.mod_version}.md")
            releaseType = 'release'
            addGameVersion "${libs.versions.minecraft}"
            addGameVersion "Java 17"
            addGameVersion "Quilt"
            relations {
                requiredDependency 'cloth-config'
                requiredDependency 'qsl'
                optionalDependency 'modmenu'
            }

            mainArtifact(remapJar.archivePath) {
                displayName = "Alaska Native Craft v${project.mod_version}"
            }

            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
        }

        options {
            javaVersionAutoDetect = false
            forgeGradleIntegration = false
        }
    }
}
